#########################################
#                                       #
# This is Chad Lake's .zshenv file:     #
#                                       #
# This file is executed by every shell  #
# e.g.: aliases, path, env variables    #
#                                       #
#########################################


###########
# Aliases #
###########
alias pwds='gpg --decrypt ~clake/private/passes.gpg | less'
alias h='history'
alias rm='rm -i'
alias ls='ls -F'
alias git_branch='(command git symbolic-ref -q HEAD || command git name-rev --name-only --no-undefined --always HEAD) 2>/dev/null'
# Mac specific stuff
if [[ $(uname -s) == "Darwin" ]]; then
    alias ls='ls -FG'
    alias subl='/Applications/Sublime\ Text.app/Contents/SharedSupport/bin/subl'
    alias update_mac_software='/usr/bin/sudo /usr/sbin/softwareupdate -ir --verbose'
    # let's use python3
    if [ -x /usr/local/bin/python3 ]; then
       alias python='/usr/local/bin/python3'
       alias py='/usr/local/bin/python3'
       alias pip='/usr/local/bin/pip3'
    fi
    # Flush DNS cache
    alias flushdns="dscacheutil -flushcache"
    # Recursively delete `.DS_Store` files
    alias cleanup_dsstore="find . -name '*.DS_Store' -type f -ls -delete"
fi
# not an alias, but we're overriding a builtin so it makes sense to put it here
function cd () {
  builtin cd "$@" && ls
}

## LinkedIn aliases
function myorg () {

   local join_str=" "

   while getopts ':e' arg; do
      case $arg in 
         e)  join_str="@linkedin.com; " ;; 
         \?) print "Usage: $0 [ username ] [ -e ]" && exit 1 ;;
      esac
   done
   shift $OPTIND-1 

   users=$(/usr/local/linkedin/bin/inchart -X -c - ${1:-clake} | tail +2 | cut -d, -f 3)

   local IFS=$'\n'
   if [ $ZSH_VERSION ]; then
      setopt sh_word_split
   fi
   for user in $users; do
      echo -n "${user}${join_str}"
   done
   echo

}




#############
# functions #
#############
# turn images into thumbnails
blogify() {
   for file in $*; do
      /usr/bin/sips -Z 800 -s format jpeg $file --out ${file%.*}.TN.jpg
   done
}

# load up ssh keys
kch() {
   local kch=`type -p keychain`;
   if [ -n "$kch" ]; then
       $kch --host $HOST;
       if [ -f ~/.keychain/${HOST}-sh ]; then
           echo "Sourcing ~/.keychain/${HOST}-sh...";
           . ~/.keychain/${HOST}-sh;
           for key in lackhead_rsa;
           do
               if ! ssh-add -l | grep -q $key; then
                   ssh-add $HOME/.ssh/$key;
               fi;
           done;
       else
           echo "No keychain host file found for $HOST.";
       fi;
   else
       echo "keychain script was not found!";
   fi
}

# add/remote a directory from PATH
pathmunge() {
   #
   # pathmunge: add a new entry to the PATH variable
   #
   # Usage:
   #   pathmunge <dir> [before]
   #
   #      This appends <dir> to the path array; if a second argument exists and is the string "before"
   #      then <dir> is prepended instead of appended.
   #
   #   pathmunge -r <dir>
   #
   #      In this usage the directory is removed from the path if it exists.
   #

   # do we have anything to do?
   if [[ $# -eq 0 ]]; then
      return
   fi

   # Are we removing or not?
   if [[ $1 == "-r" ]]; then
      shift
      local remove=1
   fi

   # make sure directory exists and is not already in PATH
   # before adding it in.
   local dirtoadd=$1
   local position=$2
   if [[ -d $dirtoadd ]]; then

       # remove any previous occurrence of $dirtoadd
       path=("${(@)path:#${dirtoadd}}")

       # exit if that's all they wanted us to do
       if [[ $remove -eq 1 ]]; then
          return
       fi

       # add the new entry; note that I am using () expansion so that
       # any null entries are removed (possibly created in the line above)
       if [[ "$position" == "before" ]]; then
          path=($dirtoadd $path)
       else
          path=($path $dirtoadd)
       fi

       # de-dupe just in case
       typeset -gU path

   else

       echo "No such directory: $1"
       return 1

   fi
}




##############
# PATH stuff #
##############
# Note that Library/Python/3.6/bin is for homebrew installed python3 on MacOS
for dir in /usr/local/linkedin/bin /usr/local/{s,}bin ~/Library/Python/3.6/bin ~/bin; do
   if [[ -d $dir ]]; then
      pathmunge $dir before
   fi
done




################################
# Set up environment variables #
################################
# Make less case-insensitive
export LESS="-qeRiFX"

# Tell git not to use my account creds for public repositories
export HOMEBREW_NO_GITHUB_API=1



